import os

rule apply_cuts_prefit_MC:
    '''
    Apply BDT and combined pid cuts before mass fit of MC
    '''
    input:
        script = "tools/apply_selection.py",
        cuts = "massfit/MC/{mode}/cuts.yaml",
        files = rules.mva_apply_MC.output.files,
    output:
        files = output_path("massfit/MC/{mode}/prefit/MC_Run2_prefit.root"),
        logs = output_path("massfit/MC/{mode}/prefit/logs/MC_Run2_prefit.log")
    shell:
        'python3 {input.script} --input-file {input.files} \
                                --output-file {output.files} \
                                --mode {wildcards.mode} \
                                --selection-files {input.cuts} | tee {output.logs}'

rule massfit_MC:
    '''
    Mass fit of MC
    '''
    input:
        script = "tools/run_fitmc.py",
        files = rules.apply_cuts_prefit_MC.output.files,
    params:
        method = lambda wildcards: config["massfit"][wildcards.mode]["method"],
    output:
        funcs = output_path("massfit/MC/{mode}/mc_shape.txt"),
        files = output_path("massfit/MC/{mode}/fit_Bmass.pdf"),
        logs = output_path("massfit/MC/{mode}/logs/fit_Bmass.log"),
    shell:
        'python3 {input.script} --input-files {input.files} \
                                --out-func {output.funcs} \
                                --output-files {output.files} \
                                --mode {wildcards.mode} \
                                --method {params.method} | tee {output.logs}'



rule apply_cuts_prefit_data:
    '''
    Apply BDT cuts before mass fit of data.
    '''
    input:
        script = "tools/apply_selection.py",
        cuts = "massfit/data/{mode}/cuts.yaml",
        files = rules.mva_apply_data.output.files,
    output:
        files = output_path("massfit/data/{mode}/prefit/data_Run2_prefit.root"),
        logs = output_path("massfit/data/{mode}/prefit/logs/data_Run2_prefit.log")
    shell:
        'python3 {input.script} --input-file {input.files} \
                                --output-file {output.files} \
                                --mode {wildcards.mode} \
                                --selection-files {input.cuts} | tee {output.logs}'

rule fit_data_sw:
    '''
    S-Fit the data sample.
    '''
    input:
        script = "tools/run_fitdata.py",
        files = rules.apply_cuts_prefit_data.output.files,
        funcs = rules.massfit_MC.output.funcs,
    params:
        method = lambda wildcards: config["massfit"][wildcards.mode]["method"],
    output:
        files = output_path("massfit/data/{mode}/data_sw.root"),
        logs = output_path("massfit/data/{mode}/logs/fit_Bmass.log"),
        funcs = output_path("massfit/data/{mode}/data_shape.txt"),
        cfit_figs = output_path("massfit/data/{mode}/fit_Bmass.pdf"),
    shell:
        'python3 {input.script} --input-file {input.files} \
                                --in-func {input.funcs} \
                                --mode {wildcards.mode} \
                                --out-func {output.funcs} \
                                --cfit-figs {output.cfit_figs} \
                                --method {params.method} \
                                --output-files {output.files} | tee {output.logs}'

